#Norsk oversettelse av FreewareTips

{
	timerate { rate 0 }
	labels { clear "planets|minorplanets|stars|constellations" }
	renderflags { set "stars|planets"
                      clear "constellations|orbits|cloudmaps" }
	print { text "Starter demo . . .\nTast ESC for å avslutte." origin "center" duration 2 }
	wait { duration 2.0 }

	print { text "Vår reise begynner nær vår hjemplanet . . ." row -3 }
	select { object "Sol/Earth" }
	cancel {}
	# goto { time 0 distance 3 upframe "ecliptical" }
	# gotolonglat { time 0 distance 3 longitude -122 latitude 47 }
	synchronous {}
	gotoloc { time 0.0 position [ 0 0 20000 ] }
	wait { duration 0.1 }
	center { time 0.1 }
	wait { duration 1.0 }
	follow {}

	print { text "Vi kretser nå ca. 12500 km over jorden." row -3 duration 5 }
	orbit { axis [ 0 1 0 ] rate 30 duration 10 }
	print { text "Jorden ser mye mer kjent ut med skyer." row -3}
	wait { duration 0.1 }
	renderflags { set "cloudmaps" }
	orbit { axis [ 0 1 0 ] rate 30 duration 6 }

	print { text "Neste stopp: Månen." row -3 }
	select { object "Moon" }
	goto { time 5 distance 4 upframe "equatorial" }
	wait { duration 5.5 }
	print { text "Hold utkikk etter jorden og solen mens vi kretser rundt månen." row -3}
	orbit { axis [ 0 1 0 ] rate 30 duration 10 }
	
	print { text "Mot solen!" row -3}
	select { object "Sol" }
	goto { time 8 distance 12 upframe "equatorial" up [ 0 1 0 ] }
	wait { duration 8.5 }
	print { text "På denne avstanden er mørke solflekker synlige på solens overflate." row -3}
	orbit { axis [ 0 1 0 ] rate 20 duration 10 }

	print { text "La oss zoome ut for å få et overblikk over det indre solsystemet." row -3}
	orbit { axis [ 1 0 0 ] rate 45 duration 2 }
	renderflags { set "orbits" }
	changedistance { duration 4.0 rate 1.0 }
	
	print { text "Aktiverer planetpåskrifter . . ." row -3}
	labels { set "planets" }
	wait { duration 1.0 }
	print { text "Ved å akselerere tiden kan vi se planetene bevege seg rundt solen." row -3}
	timerate { rate 2592000 }
	wait { duration 3.0 }
	print { text "For hvert sekund, passerer en måned i simuleringen." row -3}
	wait { duration 12.0 }
	timerate { rate 1 }
	print { text "Nå har tiden stoppet helt." row -3}

	wait { duration 1.0 }
	print { text "Vårt neste mål er Saturn." row -3}
	select { object "Saturn" }
	center { time 2 }
	wait { duration 2 }
	goto { time 8 distance 6 up [ 0 1 0 ] upframe "equatorial" }
	wait { duration 6.5 }
	renderflags { clear "orbits" }
	labels { clear "planets" }
	print { text "Flere av Saturns måner er synlige som lyse prikker." row -3 duration 3}
	orbit { axis [ 0 1 0 ] rate 30 duration 12 }
	
	select { object "Mimas" }
	goto { time 5 distance 4 upframe "equatorial" }
	print { text "Den mest framtredende formasjonen på Saturnmånen Mimas\ner det enorme nedslagskratere Herschel." row -3 duration 9 }
	orbit { axis [ 0 1 0 ] rate 30 duration 12 }
	changedistance { duration 6.0 rate 0.5 }

	select { object "Sol" }
	center { time 2 }
	print { text "Se hvor svak solen er på denne avstanden." row -3 }
	wait { duration 2 }

	print { text "La oss ta en titt på himmelens stjerner." row -3 duration 2 }
	wait { duration 2 }
	select { object "Alpha UMa" }
	center { time 2 }
	print { text "Innbyggere på nordlige halvkule bør gjenkjenne Karlsvogna i stjernebilde Store Bjørn." row -3 duration 3 }
	wait { duration 4 }

	select { object "Polaris" }
	center { time 2 }
	wait { duration 2 }
	print { text "Vi befinner oss nå rett over Polaris, Polarstjernen." row -3}
	wait { duration 1 }
	labels { set "stars" }
	wait { duration 2 }
	print { text "Polaris er en del av stjernebilde som vanligvis kalles Lille Bjørn." row -3}
	wait { duration 2 }
	print { text "For å hjelpe oss med orienteringen på himmelen, kan Celestia tegne stjernbildediagram." row -3}
	renderflags { set "constellations" }
	wait { duration 2 }
	print { text ". . . og vise stjernbildenes navn." row -3}
	labels { set "constellations" }
	wait { duration 2 }

	select { object "Alnilam" }
	center { time 4 }
	wait { duration 2 }
	print { text "Orion er himmelens mest framtredende stjernbilde." row -3 duration 3 }
	wait { duration 4 }

	select { object "Beta Cru" }
	center { time 4 }
	wait { duration 2 }
	print { text "Det sørlige korset er et velkjent syn på sørlige halvkules himmel." row -3 duration 4 }
	wait { duration 4 }

	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 7 }
	print { text "La oss aktivere visningen av galakser slik at vi kan se Melkeveien." row -3 duration 4 }
	renderflags { set "galaxies" }
	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 14 }
	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 10 }

	select { object "Antares" }
	center { time 5 }
	wait { duration 3 }
	print { text "Nå skal vi reise til Antares, en rød kjempestjerne i stjernebilde Skorpionen." row -3 duration 5 }
	wait { duration 2 }
	renderflags { clear "constellations" }
	labels { clear "constellations|stars" }
	wait { duration 1 }
	goto { time 8 distance 200 }
	wait { duration 8.5 }
	goto { time 5 distance 10 }
	wait { duration 5.0 }
	print { text "Selv om vi nå er 10 ganger lengre bort fra Antares enn hva\njorden er fra solen, ruver fremdeles denne massive røde kjempen på himmelen." row -3}
	wait { duration 4.0 }

	print { text "Nå zoomer vi ut for å få det store bilde . . ." row -3}
	changedistance { duration 10.0 rate 2.0 }

	select { object "Milky Way" }
	print { text "Vi ser nå hele Melkeveien." row -3 duration 6 }
	orbit { axis [ 1 0 0 ] rate 30 duration 16.0 }
	print { text "På tide å reise hjem . . ." row -3}

	select { object "Sol/Earth" }
	goto { time 20 distance 10 upframe "equatorial" }
	wait { duration 20.0 }
	print { text "Demo avsluttet." row -3}
	wait { duration 3.0 }
}
