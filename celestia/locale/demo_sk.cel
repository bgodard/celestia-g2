{
	timerate { rate 0 }
	labels { clear "planets|minorplanets|stars|constellations" }
	renderflags { set "stars|planets"
                      clear "constellations|orbits|cloudmaps" }
	print { text "Spúšťa sa prehliadka . . .\nUkončíte ju stlačením klávesy ESC." origin "center" duration 2 }
	wait { duration 2.0 }

	print { text "Začneme takmer doma . . ." row -3 }
	select { object "Sol/Earth" }
	cancel {}
	# goto { time 0 distance 3 upframe "ecliptical" }
	# gotolonglat { time 0 distance 3 longitude -122 latitude 47 }
	synchronous {}
	gotoloc { time 0.0 position [ 0 0 20000 ] }
	wait { duration 0.1 }
	center { time 0.1 }
	wait { duration 1.0 }
	follow {}

	print { text "Práve obiehame 12500 km nad Zemou" row -3 duration 5 }
	orbit { axis [ 0 1 0 ] rate 30 duration 10 }
	print { text "Zem vyzerá oveľa prirodzenejšie s oblakmi." row -3}
	wait { duration 0.1 }
	renderflags { set "cloudmaps" }
	orbit { axis [ 0 1 0 ] rate 30 duration 6 }

	print { text "Ďalšia zastávka: Mesiac." row -3 }
	select { object "Moon" }
	goto { time 5 distance 4 upframe "equatorial" }
	wait { duration 5.5 }
	print { text "Kým obiehame okolo Mesiaca, všimnite si Slnko a Zem." row -3}
	orbit { axis [ 0 1 0 ] rate 30 duration 10 }
	
	print { text "Vydáme sa smerom k Slnku." row -3}
	select { object "Sol" }
	goto { time 8 distance 12 upframe "equatorial" up [ 0 1 0 ] }
	wait { duration 8.5 }
	print { text "Z tejto vzdialenosti sú viditeľné na povrchu Slnka tzv. slnečné škvrny." row -3}
	orbit { axis [ 0 1 0 ] rate 20 duration 10 }

	print { text "Trocha sa vzdialime, aby bolo vidieť celú našu slnečnú sústavu." row -3}
	orbit { axis [ 1 0 0 ] rate 45 duration 2 }
	renderflags { set "orbits" }
	changedistance { duration 4.0 rate 1.0 }
	
	print { text "Zapneme názvy planét . . ." row -3}
	labels { set "planets" }
	wait { duration 1.0 }
	print { text "Môžeme zrýchliť plynutie času, aby bolo vidieť obehy planét okolo Slnka." row -3}
	timerate { rate 2592000 }
	wait { duration 3.0 }
	print { text "Každá sekunda v tejto simulácii predstavuje mesiac skutočného času." row -3}
	wait { duration 12.0 }
	timerate { rate 1 }
	print { text "Teraz sme čas úplne zastavili." row -3}

	wait { duration 1.0 }
	print { text "Ďalším našim cieľom bude Saturn." row -3}
	select { object "Saturn" }
	center { time 2 }
	wait { duration 2 }
	goto { time 8 distance 6 up [ 0 1 0 ] upframe "equatorial" }
	wait { duration 6.5 }
	renderflags { clear "orbits" }
	labels { clear "planets" }
	print { text "Niekoľko mesiacov Saturnu vidíme ako svetlé bodky." row -3 duration 3}
	orbit { axis [ 0 1 0 ] rate 30 duration 12 }
	
	select { object "Mimas" }
	goto { time 5 distance 4 upframe "equatorial" }
	print { text "Najvýraznejším objektom na mesiaci Mimas (jeden z mesiacov Saturnu) je veľký dopadový kráter Herschel." row -3 duration 9 }
	orbit { axis [ 0 1 0 ] rate 30 duration 12 }
	changedistance { duration 6.0 rate 0.5 }

	select { object "Sol" }
	center { time 2 }
	print { text "Všimnite si aké slabé sa javí Slnko z tejto vzdialenosti." row -3 }
	wait { duration 2 }

	print { text "Porozhliadnime sa teraz po hviezdach na oblohe." row -3 duration 2 }
	wait { duration 2 }
	select { object "Alpha UMa" }
	center { time 2 }
	print { text "Obyvatelia severnej pologule môžu rozoznať Veľký voz v súhvezdí Veľkej medvedice." row -3 duration 3 }
	wait { duration 4 }

	select { object "Polaris" }
	center { time 2 }
	wait { duration 2 }
	print { text "Teraz sme zaostrili na Polárku, hviezdu severu." row -3}
	wait { duration 1 }
	labels { set "stars" }
	wait { duration 2 }
	print { text "Polárka je súčasťou skupiny hviezd známych ako Malý voz." row -3}
	wait { duration 2 }
	print { text "Kvôli lepšej orientácii na oblohe môže pre nás Celestia vykresliť aj schémy súhvezdí . . ." row -3}
	renderflags { set "constellations" }
	wait { duration 2 }
	print { text ". . . a názvy súhvezdí." row -3}
	labels { set "constellations" }
	wait { duration 2 }

	select { object "Alnilam" }
	center { time 4 }
	wait { duration 2 }
	print { text "Orion je najvýraznejším súhvezdím oblohy." row -3 duration 3 }
	wait { duration 4 }

	select { object "Beta Cru" }
	center { time 4 }
	wait { duration 2 }
	print { text "Pohľad na Južný kríž je známy z oblohy južnej pologule." row -3 duration 4 }
	wait { duration 4 }

	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 7 }
	print { text "Zapneme vykresľovanie galaxií, takže budeme môcť vidieť Mliečnu dráhu." row -3 duration 4 }
	renderflags { set "galaxies" }
	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 14 }
	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 10 }

	select { object "Antares" }
	center { time 5 }
	wait { duration 3 }
	print { text "Teraz pocestujeme na Antares, čo je červený nadobor v súhvezdí Škorpión." row -3 duration 5 }
	wait { duration 2 }
	renderflags { clear "constellations" }
	labels { clear "constellations|stars" }
	wait { duration 1 }
	goto { time 8 distance 200 }
	wait { duration 8.5 }
	goto { time 5 distance 10 }
	wait { duration 5.0 }
	print { text "Hoci je teraz Antares od nás 10-krát ďalej ako je \n vzdialenosť našej Zeme od Slnka, obrovský červený obor na oblohe aj tak vyzerá veľmi mohutne." row -3}
	wait { duration 4.0 }

	print { text "Pozrime sa na všetko z ešte väčšej diaľky . . ." row -3}
	changedistance { duration 10.0 rate 2.0 }

	select { object "Milky Way" }
	print { text "Vidíme celú Mliečnu dráhu, našu galaxiu." row -3 duration 6 }
	orbit { axis [ 1 0 0 ] rate 30 duration 16.0 }
	print { text "Je čas vrátiť sa domov . . ." row -3}

	select { object "Sol/Earth" }
	goto { time 20 distance 10 upframe "equatorial" }
	wait { duration 20.0 }
	print { text "Koniec prehliadky." row -3}
	wait { duration 3.0 }
}
