{
	timerate { rate 0 }
	labels { clear "planets|minorplanets|stars|constellations" }
	renderflags { set "stars|planets"
                      clear "constellations|orbits|cloudmaps" }
	print { text "Tiek s\u0101kta demonostr\u0101cija . . .\nLai aptur\u0113tu nopiediet ESC." origin "center" duration 2 }
	wait { duration 2.0 }

	print { text "S\u0101ksim tuv\u0101k m\u0101j\u0101m . . ." row -3 }
	select { object "Sol/Earth" }
	cancel {}
	# goto { time 0 distance 3 upframe "ecliptical" }
	# gotolonglat { time 0 distance 3 longitude -122 latitude 47 }
	synchronous {}
	gotoloc { time 0.0 position [ 0 0 20000 ] }
	wait { duration 0.1 }
	center { time 0.1 }
	wait { duration 1.0 }
	follow {}

	print { text "Tagad m\u0113s atrodamies orb\u012bt\u0101, 12,500km augstum\u0101 ap Zemi." row -3 duration 5 }
	orbit { axis [ 0 1 0 ] rate 30 duration 10 }
	print { text "Ar m\u0101ko\u0146iem gan t\u0101 izskat\u0101s daudz pierast\u0101k." row -3}
	wait { duration 0.1 }
	renderflags { set "cloudmaps" }
	orbit { axis [ 0 1 0 ] rate 30 duration 6 }

	print { text "N\u0101kam\u0101 pietura: M\u0113ness" row -3 }
	select { object "Moon" }
	goto { time 5 distance 4 upframe "equatorial" }
	wait { duration 5.5 }
	print { text "Iev\u0113rojiet Zemi un Sauli, kam\u0113r apri\u0146osim M\u0113nesi" row -3}
	orbit { axis [ 0 1 0 ] rate 30 duration 10 }
	
	print { text "Un tagad dosimies uz Saules pusi" row -3}
	select { object "Sol" }
	goto { time 8 distance 12 upframe "equatorial" up [ 0 1 0 ] }
	wait { duration 8.5 }
	print { text "\u0160\u0101d\u0101 att\u0101lum\u0101 var redz\u0113t tum\u0161os Saules plankumus" row -3}
	orbit { axis [ 0 1 0 ] rate 20 duration 10 }

	print { text "Tagad att\u0101lin\u0101simies, lai uzmestu aci iek\u0161\u0113jai Saules sist\u0113mas da\u013cai" row -3}
	orbit { axis [ 1 0 0 ] rate 45 duration 2 }
	renderflags { set "orbits" }
	changedistance { duration 4.0 rate 1.0 }
	
	print { text "Iesl\u0113dzam plan\u0113tu nosaukums . . ." row -3}
	labels { set "planets" }
	wait { duration 1.0 }
	print { text ". . . un tagad varam pa\u0101trin\u0101t laiku, lai apl\u016bkotu k\u0101 t\u0101s apri\u0146\u0137o Sauli" row -3}
	timerate { rate 2592000 }
	wait { duration 3.0 }
	print { text "Katru sekunde ir piel\u012bdzin\u0101ma veselam m\u0113nesim \u0161ai simul\u0101cij\u0101. " row -3}
	wait { duration 12.0 }
	timerate { rate 1 }
	print { text "Un tagad laiks ir aptur\u0113ts pavisam." row -3}

	wait { duration 1.0 }
	print { text "N\u0101kamais pieturas punkts ir Saturns" row -3}
	select { object "Saturn" }
	center { time 2 }
	wait { duration 2 }
	goto { time 8 distance 6 up [ 0 1 0 ] upframe "equatorial" }
	wait { duration 6.5 }
	renderflags { clear "orbits" }
	labels { clear "planets" }
	print { text "Vair\u0101ki Saturna pavado\u0146i ir saskat\u0101mi k\u0101 spo\u017ei punkti ap to." row -3 duration 3}
	orbit { axis [ 0 1 0 ] rate 30 duration 12 }
	
	select { object "Mimas" }
	goto { time 5 distance 4 upframe "equatorial" }
	print { text "Uz Saturna pavado\u0146a Mimasa, visiev\u0113rojam\u0101kais ir milz\u012bgais kr\u0101teris Her\u0161els" row -3 duration 9 }
	orbit { axis [ 0 1 0 ] rate 30 duration 12 }
	changedistance { duration 6.0 rate 0.5 }

	select { object "Sol" }
	center { time 2 }
	print { text "Paskatieties cik Saule ir bl\u0101va no \u0161\u0101da att\u0101luma" row -3 }
	wait { duration 2 }

	print { text "Tagad paskat\u012bsimies uz zvaigzn\u0113m" row -3 duration 2 }
	wait { duration 2 }
	select { object "Alpha UMa" }
	center { time 2 }
	print { text "J\u016bs dro\u0161ie vien atpaz\u012bsiet Lielos Greizos Ratus jeb Lielo L\u0101ci" row -3 duration 3 }
	wait { duration 4 }

	select { object "Polaris" }
	center { time 2 }
	wait { duration 2 }
	print { text "Tagad esam iecentr\u0113ju\u0161i Pol\u0101rzvaigzni (Polaris)." row -3}
	wait { duration 1 }
	labels { set "stars" }
	wait { duration 2 }
	print { text "Pol\u0101rzvaigznes pieder pie zvaig\u017e\u0146u sakopojuma ko taut\u0101 sauc par Mazajiem Greizajiem Ratiem" row -3}
	wait { duration 2 }
	print { text "Lai lab\u0101k pal\u012bdz\u0113tu orient\u0113ties, Celestia var izz\u012bm\u0113t zvaigzn\u0101ju apveidus . . ." row -3}
	renderflags { set "constellations" }
	wait { duration 2 }
	print { text ". . . un nosaukumus" row -3}
	labels { set "constellations" }
	wait { duration 2 }

	select { object "Alnilam" }
	center { time 4 }
	wait { duration 2 }
	print { text "Orions ir visiev\u0113rojam\u0101kais zvaigzn\u0101js m\u016bsu plan\u0113tas debes\u012bs" row -3 duration 3 }
	wait { duration 4 }

	select { object "Beta Cru" }
	center { time 4 }
	wait { duration 2 }
	print { text "Dienvidu Krustus ir paz\u012bstams apveids Dienvidu puslod\u0113 dz\u012bvojo\u0161ajiem." row -3 duration 4 }
	wait { duration 4 }

	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 7 }
	print { text "Tagad iesl\u0113gsim galaktiku izz\u012bm\u0113\u0161anu, lai var\u0113tu apl\u016bkot Piena Ce\u013cu" row -3 duration 4 }
	renderflags { set "galaxies" }
	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 14 }
	rotate { axis [ 0.707 0.707 0 ] rate 20 duration 10 }

	select { object "Antares" }
	center { time 5 }
	wait { duration 3 }
	print { text "Aizce\u013cosim tagad l\u012bdz zvaigznei Antares, kas ir sarkanais p\u0101rmilzis Skorpiona zvaigzn\u0101j\u0101." row -3 duration 5 }
	wait { duration 2 }
	renderflags { clear "constellations" }
	labels { clear "constellations|stars" }
	wait { duration 1 }
	goto { time 8 distance 200 }
	wait { duration 8.5 }
	goto { time 5 distance 10 }
	wait { duration 5.0 }
	print { text "Lai gan tagad m\u0113s atrodamies 10x t\u0101l\u0101k no Antares\nnek\u0101 zeme no Saules, milz\u012bg\u0101 sarkan\u0101 zvaigznes joproj\u0101m aiz\u0146em liel\u0101ku da\u013cu skata." row -3}
	wait { duration 4.0 }

	print { text "Tagad att\u0101lin\u0101simies atkal, lai apl\u016bkotu kopainu . . ." row -3}
	changedistance { duration 10.0 rate 2.0 }

	select { object "Milky Way" }
	print { text "Tagad m\u0113s redzam visu Piena Ce\u013ca galaktiku no malas." row -3 duration 6 }
	orbit { axis [ 1 0 0 ] rate 30 duration 16.0 }
	print { text "Laiks doties m\u0101j\u0101s . . ." row -3}

	select { object "Sol/Earth" }
	goto { time 20 distance 10 upframe "equatorial" }
	wait { duration 20.0 }
	print { text "Demonstr\u0101cija pabeigta." row -3}
	wait { duration 3.0 }
}
